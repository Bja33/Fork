
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды 
	
	// {{Данилов 
	// Вызов серверной процедуры, для вставки "ПоляВвода" "КонтактноеЛицо"	
	ДР_РаботаСФормойВызовСервера.ПриСозданииНаСервере(ЭтотОбъект);
	
	// Добавление "ОбычнойГруппы" "ГруппаСкидка"
	ГруппаСкидка = Элементы.Добавить("ГруппаСкидка", Тип("ГруппаФормы"), Элементы.ГруппаШапкаЛево);
	ГруппаСкидка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСкидка.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаСкидка.ОтображатьЗаголовок = ЛОЖЬ;
	ГруппаСкидка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда; 
	
	// Вставка "ПоляВвода" "СогласованнаяСкидка"
	ПолеСогласованнаяСкидка = Элементы.Вставить("ДР_СогласованнаяСкидка", Тип("ПолеФормы"), Элементы.ГруппаСкидка,);
	ПолеСогласованнаяСкидка.ПутьКДанным = "Объект.ДР_СогласованнаяСкидка";
	ПолеСогласованнаяСкидка.Вид = ВидПоляФормы.ПолеВвода;
	ПолеСогласованнаяСкидка.УстановитьДействие("ПриИзменении", "ДР_СогласованнаяСкидкаПриИзменении");
	//@skip-check wrong-string-literal-content
	ПолеСогласованнаяСкидка.УстановитьДействие("ОкончаниеВводаТекста", "ДР_СогласованнаяСкидкаОкончаниеВводаТекста");

	//Добавление команды "ДР_ПересчитатьТаблицу"
	НоваяКоманда = Команды.Добавить("ДР_ПересчитатьТаблицу");
	НоваяКоманда.Заголовок = "Пересчитать таблицу"; 
	НоваяКоманда.Действие = "ДР_ПересчитатьТаблицу";
	
	Кнопка = Элементы.Добавить("ДР_ПересчитатьТаблицу", Тип("КнопкаФормы"), Элементы.ГруппаСкидка);
	Кнопка.ИмяКоманды = "ДР_ПересчитатьТаблицу";
	// Данилов}}
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные); 
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
		
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные); 

КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	
	РассчитатьСуммуДокумента();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	// {{Данилов
	//КоэффициентСкидки = 1 - ТекущиеДанные.Скидка / 100; 
	КоэффициентСкидки = 1 - (ТекущиеДанные.Скидка + Объект.ДР_СогласованнаяСкидка) / 100;
	
	Если КоэффициентСкидки > 1 ИЛИ КоэффициентСкидки < 0 Тогда
		ТекущиеДанные.Скидка = 0;
		КоэффициентСкидки = 1 - (ТекущиеДанные.Скидка + Объект.ДР_СогласованнаяСкидка) / 100;
		ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;
		ДР_ПредупреждениеПревышенияСкидки();
		Возврат;	
	КонецЕсли;
	// Данилов}}
	
	ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

//@skip-check module-unused-method
&НаКлиенте
Процедура ДР_ПересчитатьТаблицу(Команда)
	ДР_ИзменениеСогласованнойСкидки();	
КонецПроцедуры

&НаКлиенте
Процедура ДР_ИзменениеСогласованнойСкидки()
	
	Для Каждого Строка Из Объект.Товары Цикл
		РассчитатьСуммуСтроки(Строка);
	КонецЦикла;
	
	Для Каждого Строка Из Объект.Услуги Цикл
		РассчитатьСуммуСтроки(Строка);
	КонецЦикла;
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры  

&НаКлиенте
Асинх Процедура ДР_СогласованнаяСкидкаПриИзменении(Элемент)
		
	Если НЕ ЗначениеЗаполнено(Объект.Товары) И НЕ ЗначениеЗаполнено(Объект.Услуги) Тогда 
		Возврат;
	Иначе 
		
		Обещание = ВопросАсинх(НСтр("ru='Изменен процент скидки. Пересчитать таблицы товаров и услуг?'"), 
					РежимДиалогаВопрос.ДаНет,, 
					КодВозвратаДиалога.Да);
		Результат = Ждать Обещание;
		
		Если Результат = КодВозвратаДиалога.Да Тогда			
			ДР_ИзменениеСогласованнойСкидки(); 		
		КонецЕсли;
		
	КонецЕсли;		
	
КонецПроцедуры 

&НаКлиенте
Процедура ДР_ПредупреждениеПревышенияСкидки()	
	ОбщегоНазначенияКлиент.СообщитьПользователю(
   		НСтр("ru = 'Общая скидка не должна быть больше 100%!'"), ,
   		"ДР_СогласованнаяСкидка",
   		"Объект");	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
